plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'net.gradleutil.gradle-conf' version '1.1.9'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

/*
confConfig {
    schemaFile.set file('src/main/resources/schema/Version.schema.json')
    conf.set file('src/main/resources/config.json')
    //outputDirectory = new File('/tmp/model')
    allowUnresolved = false
    useSystemEnvironment = true
    generateBean = true
    packageName = 'net.gradleutil.version'
    silent = false
}
*/

dependencies {
    implementation 'com.github.zafarkhaja:java-semver:0.9.0'
    implementation 'org.ajoberstar.grgit:grgit-core:5.0.0'

    testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
}

gradlePlugin {
    plugins {
        gradleversion {
            id = 'net.gradleutil.gradle-version'
            implementationClass = 'net.gradleutil.gradleversion.GradleVersionPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/gradleutil/gradle-version'
    vcsUrl = 'https://github.com/gradleutil/gradle-version'
    description = 'Simple semver version bumper based on branch matching and git dirty or not'
    tags = ['build-tool', 'versioning' ]

    plugins {
        gradleversion {
            id = 'net.gradleutil.gradle-version'
            displayName = 'Semver from Git status'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    useJUnitPlatform()
}
